#lang ivy1.7

type value
type quorum
type node

relation member(N:node, Q:quorum)
axiom forall Q1, Q2. exists N. member(N, Q1) & member(N, Q2)

relation vote_request_msg(N:node, M:node)
relation voted(N:node)
relation vote_msg(N:node, M:node)
relation votes(N:node, M:node)
relation leader(N:node)
relation decided(N:node, V:value)

after init {
    vote_request_msg(N1, N2)  := false;
    voted(N) := false;
    vote_msg(N1, N2) := false;
    votes(N1, N2) := false;
    leader(N1) := false;
    decided(N, V) := false;

}

action send_request_vote(src: node, dst: node) = {
    vote_request_msg(src, dst) := true;
}

action send_vote(src: node, dst: node ) = {
    require ((~voted(src)) & ((vote_request_msg(dst, src))));
    vote_msg(src, dst) := true;
    voted(src) := true;
}

action send_vote_mark_false(src: node, dst: node ) = {
    require ((~voted(src)) & ((vote_request_msg(dst, src))));
    vote_msg(src, dst) := true;
    voted(src) := true;
    vote_request_msg(dst, src) := false;
}

action recv_vote(n: node, sender: node ) = {
    require (vote_msg(sender, n));
    votes(n, sender) := true;
}

action become_leader(n: node, q: quorum ) = {
    require (member(N, q) -> (votes(n, N)));
    leader(n) := true;
}

action decide(n: node, v: value ) = {
    require ((leader(n)) & ~(decided(n, V)));
    decided(n, v) := true;
}

export send_request_vote
export send_vote
export send_vote_mark_false
export recv_vote
export become_leader
export decide

# safety property
invariant [1000000] decided(N1, V1) & decided(N2, V2) -> V1 = V2
