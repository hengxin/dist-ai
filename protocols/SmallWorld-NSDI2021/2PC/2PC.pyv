sort node

# epochs are totally ordered with a least element called zero
mutable relation vote_yes(node)
mutable relation vote_no(node)
mutable relation alive(node)
mutable relation go_commit(node)
mutable relation go_abort(node)
mutable relation decide_commit(node)
mutable relation decide_abort(node)

mutable relation abort_flag()

init !vote_yes(N)
init !vote_no(N)
init alive(N)
init !go_commit(N)
init !go_abort(N)
init !decide_commit(N)
init !decide_abort(N)
init !(abort_flag)

transition vote1(n: node)
  modifies vote_yes
  & old(
    & alive(n)
    & !vote_no(n)
    & !decide_commit(n)
    & !decide_abort(n)
  )
  & (forall N . vote_yes(N) <-> old(vote_yes(N)) | N=n)

transition vote2(n: node)
  modifies vote_no, abort_flag, decide_abort
  & old(
    & alive(n)
    & !vote_yes(n)
    & !decide_commit(n)
    & !decide_abort(n)
  )
  & (forall N . vote_no(N) <-> old(vote_no(N)) | N=n)
  & abort_flag
  & (forall N . decide_abort(N) <-> old(decide_abort(N)) | N=n)

transition fail(n: node)
  modifies alive, abort_flag
  & old(alive(n))
  & (forall N . alive(N) <-> old(alive(N)) & N != n)
  & abort_flag

transition go1()
  modifies go_commit
  & (forall N .
    old(
      & !go_commit(N)
      & !go_abort(N)
      & vote_yes(N)
    )
  )
  & (forall N . go_commit(N))

transition go2()
  modifies go_abort
  & (forall N .
    old(
      & !go_commit(N)
      & !go_abort(N)
    )
  )
  & (exists N. old(vote_no(N) | !alive(N)))
  & (forall N . go_abort(N))

transition commit(n: node)
  modifies decide_commit
  & old(
    & alive(n)
    & go_commit(n)
  )
  & (forall N . decide_commit(N) <-> old(decide_commit(N)) | N=n)

transition abort(n: node)
  modifies decide_abort
  & old(
    & alive(n)
    & go_abort(n)
  )
  & (forall N . decide_abort(N) <-> old(decide_abort(N)) | N=n)

#safety !((!(alive(N0)) & !(abort_flag))) 
#safety !((!(abort_flag) & vote_no(N0))) 
#safety !((!(abort_flag) & go_abort(N0))) 
#safety !((!(go_abort(N0)) & decide_abort(N0) & vote_yes(N0))) 
#safety !((!(go_abort(N5)) & decide_abort(N5) & vote_yes(N5))) 
#safety (N1 != N5) -> !((!(go_abort(N5)) & go_abort(N1))) 
#safety !((!(go_commit(N5)) & decide_commit(N5))) 
#safety (N0 != N5) -> !((!(go_commit(N5)) & go_commit(N0))) 
#safety (N0 != N5) -> !((!(go_commit(N5)) & decide_commit(N0))) 
#safety (N4 != N5) -> !((!(vote_yes(N4)) & go_commit(N5))) 
#safety !((!(vote_yes(N5)) & go_commit(N5))) 
#safety (N0 != N5) -> !((go_commit(N5) & go_abort(N0))) 
#safety !((go_commit(N5) & go_abort(N5))) 

# the safety properties:
safety (decide_commit(N) -> !decide_abort(N2))
safety (decide_commit(N) -> vote_yes(N2))
safety (decide_abort(N) -> abort_flag)
